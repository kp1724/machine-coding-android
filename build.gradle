import org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile
import org.jetbrains.kotlin.gradle.tasks.KaptGenerateStubs

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        compileSdk = 34
        buildTools = '31.0.0'
        minSdk = 24
        targetSdk = 31

        /*https://blog.jetbrains.com/kotlin/category/releases/*/
        kotlinVersion = '1.9.20'
        /*https://github.com/Kotlin/kotlinx.coroutines/releases*/
        coroutinesVersion = '1.7.3'
        /*https://developer.android.com/jetpack/androidx/releases/appcompat*/
        appCompatVersion = '1.6.1'
        /*https://developer.android.com/jetpack/androidx/releases/core*/
        androidxCoreVersion = '1.7.0'
        /*https://developer.android.com/jetpack/androidx/releases/activity*/
        androidxActivityVersion = '1.4.0'
        /*https://developer.android.com/jetpack/androidx/releases/fragment*/
        androidxFragmentVersion = '1.4.1'
        kotlinDep = [
                gradlePlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}",
                stdlib      : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}",
                coroutines  : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}",
        ]

        androidDependencies = [
                /*https://developer.android.com/studio/releases/gradle-plugin*/
                gradlePlugin: "com.android.tools.build:gradle:7.1.2",
                /*https://dl.google.com/dl/android/maven2/index.html*/
                appCompat   : "androidx.appcompat:appcompat:${appCompatVersion}",
        ]

        /*https://github.com/google/dagger#dagger*/
        daggerVersion = '2.48.1'
        daggerDep = [
                core    : "com.google.dagger:dagger:${daggerVersion}",
                compiler: "com.google.dagger:dagger-compiler:${daggerVersion}",
        ]

        /*https://dl.google.com/dl/android/maven2/index.html*/
        archComponentsVersion = '2.6.2'
        archComponents = [
                viewModelKtx      : "androidx.lifecycle:lifecycle-viewmodel-ktx:${archComponentsVersion}",
        ]

        /*https://github.com/square/okhttp/blob/master/CHANGELOG.md*/
        okhttpVersion = '5.0.0-alpha.2'
        /*https://github.com/square/retrofit/blob/master/CHANGELOG.md*/
        retrofitVersion = '2.9.0'
        networking = [
                okhttpLog   : "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}",
                /*https://github.com/google/gson/releases*/
                retrofit    : "com.squareup.retrofit2:retrofit:${retrofitVersion}",
                retrofitGson: "com.squareup.retrofit2:converter-gson:${retrofitVersion}",
        ]

        ui = [
                /*https://dl.google.com/dl/android/maven2/index.html*/
                constraintLayout: 'androidx.constraintlayout:constraintlayout:2.1.3',
                recyclerview    : 'androidx.recyclerview:recyclerview:1.2.1',
                /*https://github.com/square/picasso#download*/
                picasso         : "com.squareup.picasso:picasso:2.71828",
                lottie          : "com.airbnb.android:lottie:3.3.0",
                cardview        : "androidx.cardview:cardview:1.0.0"
        ]

        testing = [
                junit               : "junit:junit:4.13.2",
                junitRunner         : "androidx.test.runner.AndroidJUnitRunner",
                coroutines          : "org.jetbrains.kotlinx:kotlinx-coroutines-test:${coroutinesVersion}"
        ]

    }

    repositories {
        google()
        mavenCentral()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.2'
        classpath kotlinDep.gradlePlugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()

    }
    // There is a compatibility issue between the latest Android Gradle plugin and Kotlin kapt.
    // As a result, the jvmTarget that you specify in the Android configuration will be set on the
    // Kotlin compilation tasks but not on the kapt task, which by default uses the toolchain version
    tasks.withType(AbstractKotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.8"
            languageVersion = "1.8"
        }
    }
    tasks.withType(KaptGenerateStubs).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
